language Education

model Education /* Computer Aided Learning */ {
    name: identifier;
    description: string; /* e.g. Mathematics for students age 10 */
    topic: Topic[];
    flow: FlowDescription[];
    tests: Test[];
}

modelunit Topic {
    name: identifier;
    description: string; /* e.g. Fractions, or Multiplications */
    pages: Page[];
}

abstract concept Page {
    name: identifier;
    questions: Question[];
    calcResult: Function; /* If the Page has no questions, the Function results in top grade */
}

concept Theory base Page {
    lines: Line[]; /* For the sake of the example this is simplified. Should be formatted text including pictures. */
}

concept Line {
    content: string;
}

concept Video base Page {
    url: string;
}

concept WorkSheet base Page {
}

concept ExamplePage base Page {
    content: string;
}

concept InDepthMaterial base Page {
    content: string;
}

concept Question {
    name: identifier;
    content: string;
    correctAnswer: RuleExpression;
}

